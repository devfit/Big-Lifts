task copyImages(type: Copy) {
    from 'images'
    into 'build/images'
    include '*.*'
}

task copyHtmlFiles(type: Copy, dependsOn: [copyImages]) {
    from '.'
    into 'build'
    include '*.html'
    include 'js/**/*.png'
}

task copyAppIcon(type: Copy) {
    from 'distribution'
    into 'build'
    include 'icons/**/*.png'
}

task copyPhonegapConfig(type: Copy){
    from '.'
    into 'build'
    include 'config.xml'
}

def compress(String type, String baseDirectory) {
    def elementsToMinify = []
    fileTree {
        from baseDirectory
        include "**/*.$type"
    }.visit { element ->
        if (element.file.isFile()) {
            elementsToMinify << element
        }
    }

    elementsToMinify.each { element ->
        println "Minifying ${element.relativePath}"
        def outputFileLocation = "build/$baseDirectory/${element.relativePath}"
        new File(outputFileLocation).parentFile.mkdirs()
        minify("$baseDirectory/${element.relativePath}", outputFileLocation)
    }
}

def minify(String input, String output) {
    ant.java(jar: "lib/yuicompressor-2.4.7.jar", fork: true) {
        arg(value: input)
        arg(value: "-o")
        arg(value: output)
    }
}

task compressJs {
    inputs.dir new File('js')
    outputs.dir new File('build/js')

    doLast {
        compress('js', 'js')
    }
}

task compressCss {
    inputs.dir new File('css')
    inputs.dir new File('js')
    outputs.dir new File('build/css')
    outputs.dir new File('build/js')

    doLast {
        compress('css', 'css')
        compress('css', 'js')
    }
}

task copyCss(type: Copy) {
    from '.'
    into 'build'
    include 'css/**/*.css'
    include 'js/**/*.css'
}

defaultTasks 'clean', 'build'

task clean << {
    delete('build')
    mkdir('build')
}

task build(dependsOn: [copyHtmlFiles, copyPhonegapConfig, copyAppIcon, compressJs, copyCss]) << {
}

task test(type: Exec) {
    commandLine = ['cucumber', 'TEST_FILE=./index.html']
    workingDir = new File('./test')
}

task testBuild(type: Exec, dependsOn: [build]) {
    commandLine = ['cucumber', 'TEST_FILE=./build/index.html']
    workingDir = new File('./test')
}

