SCRIPT_MATCHING_PATTERN = /\s+<script.*?src\s*=\s*['"](.*?)['"]><\/script>.*/

task copyHtmlFilesToBuild(type: Copy) {
    from '.'
    into 'build'
    include 'index.html'
    include 'js/**/*.png'
}

task copyAppIcon(type: Copy) {
    from '.'
    into 'build'
    include 'apple-touch-icon.png'
}

def compress(String type, String baseDirectory) {
    def elementsToMinify = []
    fileTree {
        from baseDirectory
        include "**/*.$type"
    }.visit { element ->
        if (element.file.isFile()) {
            elementsToMinify << element
        }
    }

    elementsToMinify.each { element ->
        println "Minifying ${element.relativePath}"
        def outputFileLocation = "build/$baseDirectory/${element.relativePath}"
        new File(outputFileLocation).parentFile.mkdirs()
        minify("$baseDirectory/${element.relativePath}", outputFileLocation)
    }
}

task compressJs(dependsOn: [copyHtmlFilesToBuild]) {
    inputs.dir new File('js')
    inputs.file new File('index.html')
    outputs.file new File('build/js/app.min.js')

    doLast {
        String originalIndex = new File('index.html').text
        List<File> scripts = originalIndex.findAll(SCRIPT_MATCHING_PATTERN) {m, script -> new File(script)}
        String newHtml = removeScriptsFromHtml(originalIndex)

        File jsOutputDir = new File('build', 'js')
        jsOutputDir.mkdirs()

        File combinedJsFile = new File(jsOutputDir, 'app.js')
        scripts.each { File script ->
            combinedJsFile << script.text
        }

        minify("build/js/app.js", "build/js/app.min.js")
        combinedJsFile.delete()
        newHtml = addCombinedIncludeToHtml(newHtml, 'js/app.min.js')
        new File('build', 'index.html').withWriter { writer ->
            writer.write(newHtml)
        }
    }
}

def minify(String input, String output) {
    ant.java(jar: "lib/yuicompressor-2.4.7.jar", fork: true) {
        arg(value: input)
        arg(value: "-o")
        arg(value: output)
    }
}

def removeScriptsFromHtml(String html) {
    html.replaceAll(SCRIPT_MATCHING_PATTERN, '')
}

def addCombinedIncludeToHtml(String html, String combinedJsFilename) {
    html.replace('</head>', "<script type='text/javascript' src='${combinedJsFilename}'></script></head>")
}

task compressCss {
    inputs.dir new File('css')
    inputs.dir new File('js')
    outputs.dir new File('build/css')
    outputs.dir new File('build/js')

    doLast {
        compress('css', 'css')
        compress('css', 'js')
    }
}

task copyCss(type: Copy) {
    from '.'
    into 'build'
    include 'css/**/*.css'
    include 'js/**/*.css'
}

defaultTasks 'clean', 'website'

task clean << {
    delete('build')
    mkdir('build')
}

task website(dependsOn: [copyHtmlFilesToBuild, copyAppIcon, compressJs, copyCss]) << {
}

task appmobi(dependsOn: [copyHtmlFilesToBuild, compressJs, copyCss]) << {
    File indexFile = new File('build/index.html')

    def appMobiInclude = '''<script type="text/javascript" src="http://localhost:58888/_appMobi/appmobi.js"></script>'''
    def appMobiLoader = '''
<script type="text/javascript">
function onDeviceReady() {
    AppMobi.device.setRotateOrientation("portrait");
    AppMobi.webroot=window.location.href.substring(0,window.location.href.lastIndexOf("/"))+"/";
    AppMobi.device.hideSplashScreen();
    wendlerApp.launch();
}
    document.addEventListener("appMobi.device.ready", onDeviceReady, false);
</script>'''

    def indexText = indexFile.text.replace("</title>", "</title>$appMobiInclude")
    indexText = indexText.replace("</head>", "$appMobiLoader</head>")
    indexFile.withWriter { writer ->
        writer.write(indexText)
    }
}

task test(type: Exec) {
    commandLine = ['cucumber', 'TEST_FILE=./index.html']
    workingDir = new File('./test')
}

task testBuild(type: Exec, dependsOn: [appmobi]) {
    commandLine = ['cucumber', 'TEST_FILE=./build/index.html']
    workingDir = new File('./test')
}

